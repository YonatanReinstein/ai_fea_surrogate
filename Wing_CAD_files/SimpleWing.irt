#############################################################################
#
# Make the wing
#
Profile1Top = cbspline( 4,
			list( ctlpt( E2, 0.0, 0.02 ),
			      ctlpt( E2, 0.15, 0.12 ),
			      ctlpt( E2, 0.6, 0.07 ),
			      ctlpt( E2, 1.0, 0.02 ) ),
			list( kv_open ) );

Profile1 = ruledSrf( Profile1Top, Profile1Top * sy( -1 ) ):

# view( list( Profile1, axes ), 1 );

Profile2 = Profile1 * sc( 0.7 ) * txyz( 0.22, 0.0, 0.2 ):

Profile2a = Profile1 * sc( 0.58 ) * txyz( 0.39, 0.0, 0.6 ):

Profile3 = Profile1 * sc( 0.55 ) * txyz( 0.45, 0.0, 0.8 ):

Profile4 = Profile1 * sc( 0.43 ) * txyz( 0.71, 0.0, 1.6 ):

Profile5 = Profile1 * sc( 0.25 ) * txyz( 1.05, 0.0, 2.6 );


Wing1 = tFromSrfs( list( Profile1,
		         Profile2,
		         Profile2a,
		         Profile3 ), 3, list( kv_open ) ):

Wing2 = tFromSrfs( list( Profile3,
		         Profile4,
		         Profile5 ), 3, list( kv_open ) ):

Wing12 = list( Wing1, Wing2 ):

# view( list( Wing12, axes ), 1 );

Wing = coerce( mmerge( coerce( Wing1, multivar_type ),
		       coerce( Wing2, multivar_type ), 2, false ),
	       trivar_type );

# view( Wing, 1 );

save( "Wing", Wing );

#############################################################################
#
# Make the tile
#
FacePrm = list( 0.125, 0.0, 1.0, false, 1.0, 0.3, nil(), nil(), nil() );
Tile = microtile( 1, list( FacePrm, FacePrm, FacePrm,
			   FacePrm, FacePrm, FacePrm, FALSE ) );

# view( list( tile, axes ), 1 );

#############################################################################
#
# Make the lattice
#

L = 6;
M = 2;

MS = MicroStrct( Wing, 1,
		 list( Tile, 0, 0, True, list( L, M, list( 4, 4, 16 ) ),
		       True, 0.5, 1.0, 3, False, False ) );

view( MS, 1 );

save( "MSWing", MS );

